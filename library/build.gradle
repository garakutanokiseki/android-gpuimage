apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int

        versionCode 1
        versionName VERSION_NAME

        consumerProguardFiles 'proguard-rules.txt'

        ndk {
            moduleName "gpuimage-library"
            stl "gnustl_shared"
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            ldLibs "log","GLESv2","jnigraphics"
        }

        splits {
            abi {
                enable !project.hasProperty('dbgBld')
                reset()
                include 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
                universalApk true
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jni.srcDirs = ['jni']
        }

        //instrumentTest.setRoot('tests')
    }

    lintOptions {
        abortOnError false
    }

    externalNativeBuild {
        ndkBuild {
            path 'Android.mk'
        }
    }
    buildToolsVersion '27.0.3'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

publish {
    userOrg = POM_DEVELOPER_ID
    groupId = GROUP
    artifactId = ARTIFACT_ID
    publishVersion = VERSION_NAME
    desc = POM_DESCRIPTION
    website = POM_URL
}
